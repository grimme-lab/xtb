# Maintainer: Sebastian Ehlert <awvwgk@gmail.com>

_pkgname=xtb
pkgname="${_pkgname}-git"
pkgver=6.2.2.r48.d40ebcd
pkgrel=1
arch=('x86_64')
url="https://github.com/grimme-lab/xtb"
provides=("${_pkgname}")
depends=('openblas')
makedepends=('git' 'meson' 'ninja')
conflicts=("${_pkgname}")
license=('LGPL3')
pkgdesc="Semiempirical Extended Tight-Binding Program Package"
source=("git+https://github.com/grimme-lab/xtb.git#branch=master")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  _parent_ver=$(git describe --tags --abbrev=0 | sed 's/\([^-]*-\)g/r\1/;s/-//g' | tr -d '[:alpha:]' )
  printf "%s.r%s.%s" \
         "${_parent_ver}" \
         "$(git rev-list --count HEAD)" \
         "$(git rev-parse --short HEAD)"
}

build() {
  mkdir -p "${srcdir}/${_pkgname}"/_build
  cd "${srcdir}/${_pkgname}"/_build
  meson setup . .. \
      --buildtype release \
      --warnlevel 0 \
      --prefix=/usr \
      -Dla_backend=openblas
  ninja
}

package() {
  cd "${srcdir}/${_pkgname}"/_build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
